# Maintainer: Tarmo Heiskanen <turskii@gmail.com>
# Contributor: mar77i <mar77i at mar77i dot ch>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Scytrin dai Kinthra <scytrin@gmail.com>

pkgname=st-git
pkgver=0.8.3.r14.gdec6b53
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url='https://st.suckless.org/'
arch=('i686' 'x86_64')
license=('MIT')
depends=('libxft')
makedepends=('ncurses' 'libxext' 'git')
source=('git://git.suckless.org/st')
sha1sums=('SKIP')
provides=('st')
conflicts=('st')
_gitdir=${pkgname%'-git'}
_startdir=$PWD

pkgver() {
    cd "$_gitdir"
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "$srcdir/$_gitdir"
    echo "Cleaning up git directory ..."
    git reset --hard
    git clean -xfd

    msg "Applying patches from $_startdir if they exist ..."
    if [ -d "$_startdir/patches" ]; then
        for patch in $_startdir/patches/*.diff; do
            msg2 "Applying $patch ..."
            patch -p1 -s -i "$patch"
        done;
    fi;

    echo "Copying config.def.h to $_startdir ..."
    cp config.def.h "$_startdir/"

    echo "Copying config.h from $_startdir if it exists..."
    [ -f "$_startdir/config.h" ] \
        && cp "$_startdir/config.h" . \
	|| echo 'Falling back to config.def.h...'
}

build() {
    cd "$srcdir/$_gitdir"
    make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
    cd "$srcdir/$_gitdir"
    make PREFIX=/usr DESTDIR="${pkgdir}" TERMINFO="/dev/null" install
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
