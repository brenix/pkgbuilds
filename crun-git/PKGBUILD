pkgname=crun-git
pkgver=r450.1cf4ea5
pkgrel=1
pkgdesc="A lightweight fully featured OCI runtime and C library for running containers"
url="https://github.com/giuseppe/crun"
arch=(x86_64)
license=(LGPL3)
makedepends=(git make)
depends=('yajl' 'libseccomp' 'libcap' 'libselinux')
provides=('crun-git')
conflicts=('crun' 'crun-git')
source=("$pkgname::git+https://github.com/giuseppe/crun.git")
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

build() {
  cd "${pkgname}"
  ./autogen.sh && ./configure
  make
}

package() {
  cd "${pkgname}"
  make prefix="$pkgdir/usr/" install
}